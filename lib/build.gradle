plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'de.mannodermaus.android-junit5'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 2
        versionName "0.2.0"
    }

    sourceSets.each { variant ->
        def variantName = variant.name

        variant.java.srcDir("src/$variantName/kotlin")

        if (variantName.toLowerCase().contains("test") && !variantName.toLowerCase().contains("common")) {
            variant.java.srcDirs += "src/testCommon/kotlin"
            variant.res.srcDirs += "src/testCommon/res"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    testOptions {
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }
        }

        unitTests.all {
            testLogging.events = ["passed", "skipped", "failed"]
        }
    }
}

ext {
    spek = "2.0.15"
    gson = "2.8.6"
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    api "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    api 'androidx.core:core-ktx:1.6.0'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0'
    api "com.google.code.gson:gson:$gson"
    api 'com.squareup.retrofit2:retrofit:2.9.0'

    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek"
    testImplementation "org.spekframework.spek2:spek-runner-junit5:$spek"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation "com.google.truth:truth:1.1.3"
}